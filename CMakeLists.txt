cmake_minimum_required(VERSION 3.10)
project(gwsh)
include(GNUInstallDirs)

# Make sure these macros are also available during configure checks.
list(APPEND CMAKE_C_FLAGS -D_GNU_SOURCE)
if(CMAKE_SYSTEM_NAME STREQUAL Linux AND CMAKE_SIZEOF_VOID_P EQUAL 4)
  list(APPEND CMAKE_C_FLAGS -D_FILE_OFFSET_BITS=64)
endif()

include(CMakeDependentOption)
option(DEFAULT_PRIVILEGED "Keep extra privileges when started with effective UID/GID unequal to real UID/GID" ON)
option(LINENO "Enable LINENO support" ON)
option(LOCALE "Enable locale support" ON)
cmake_dependent_option(LOCALE_FOR_PARSER "Have parsing ignore locale changes after the shell starts" ON LOCALE OFF)
option(LIBEDIT "Enable command line editing using libedit" OFF)
cmake_dependent_option(COMPLETION "Enable tab completion" ON LIBEDIT OFF)
cmake_dependent_option(COMPLETION_BUILTIN "Enable built-in tab completion" ON COMPLETION OFF)
set(SELF_EXEC_PATH_DEFAULT NO)
if(NOT CMAKE_CROSSCOMPILING AND EXISTS /proc/self/exe)
  set(SELF_EXEC_PATH_DEFAULT /proc/self/exe)
endif()
set(SELF_EXEC_PATH "${SELF_EXEC_PATH_DEFAULT}" CACHE STRING "Path to gwsh to use when re-executing")
option(TEST_WORKAROUND "Guard against faccessat(2) that tells root all files are executable" OFF)

function(configure)
  try_compile(HAVE_COMPUTED_GOTO ${CMAKE_CURRENT_BINARY_DIR}
              ${CMAKE_CURRENT_SOURCE_DIR}/cmake/computed_goto.c)
  try_compile(HAVE_GCC_ATTRIBUTE ${CMAKE_CURRENT_BINARY_DIR}
              ${CMAKE_CURRENT_SOURCE_DIR}/cmake/gcc_attribute.c)
  try_compile(HAVE_ST_MTIM ${CMAKE_CURRENT_BINARY_DIR}
              ${CMAKE_CURRENT_SOURCE_DIR}/cmake/st_mtim.c)
  try_compile(HAVE_ST_MTIMESPEC ${CMAKE_CURRENT_BINARY_DIR}
              ${CMAKE_CURRENT_SOURCE_DIR}/cmake/st_mtimespec.c)
  try_compile(FAIL_STATIC_ASSERT ${CMAKE_CURRENT_BINARY_DIR}
              ${CMAKE_CURRENT_SOURCE_DIR}/cmake/static_assert.c)
  if(NOT FAIL_STATIC_ASSERT)
    set(HAVE_STATIC_ASSERT 1)
  endif()

  include(CheckIncludeFile)
  check_include_file(alloca.h HAVE_ALLOCA_H)
  check_include_file(paths.h HAVE_PATHS_H)
  check_include_file(sys/ioctl.h HAVE_SYS_IOCTL_H)
  check_include_file(xlocale.h HAVE_XLOCALE_H)

  include(CheckSymbolExists)
  check_symbol_exists(bsearch stdlib.h HAVE_BSEARCH)
  check_symbol_exists(faccessat unistd.h HAVE_FACCESSAT)
  check_symbol_exists(getpwnam pwd.h HAVE_GETPWNAM)
  check_symbol_exists(getrlimit sys/resource.h HAVE_GETRLIMIT)
  check_symbol_exists(isalpha ctype.h HAVE_ISALPHA)
  check_symbol_exists(isblank ctype.h HAVE_DECL_ISBLANK)
  check_symbol_exists(killpg signal.h HAVE_KILLPG)
  check_symbol_exists(mempcpy string.h HAVE_MEMPCPY)
  check_symbol_exists(stpcpy string.h HAVE_STPCPY)
  check_symbol_exists(strchrnul string.h HAVE_STRCHRNUL)
  check_symbol_exists(strsignal string.h HAVE_STRSIGNAL)
  check_symbol_exists(strtod stdlib.h HAVE_STRTOD)
  check_symbol_exists(strtoimax inttypes.h HAVE_STRTOIMAX)
  check_symbol_exists(strtoumax inttypes.h HAVE_STRTOUMAX)
  check_symbol_exists(sysconf unistd.h HAVE_SYSCONF)
  check_symbol_exists(INTMAX_WIDTH stdint.h HAVE_INTMAX_WIDTH)
  check_symbol_exists(PRIdMAX inttypes.h HAVE_PRIDMAX)
  if(HAVE_PATHS_H)
    check_symbol_exists(_PATH_BSHELL paths.h HAVE_PATH_BSHELL)
    check_symbol_exists(_PATH_DEVNULL paths.h HAVE_PATH_DEVNULL)
    check_symbol_exists(_PATH_TTY paths.h HAVE_PATH_TTY)
  endif()

  if(LIBEDIT)
    function(configure_libedit)
      list(APPEND CMAKE_REQUIRED_LIBRARIES edit)
      if(COMPLETION AND NOT COMPLETION_BUILTIN)
        check_symbol_exists(_el_fn_sh_complete histedit.h HAVE__EL_FN_SH_COMPLETE)
        if(NOT HAVE__EL_FN_SH_COMPLETE)
          check_symbol_exists(_el_fn_complete histedit.h HAVE__EL_FN_COMPLETE)
        endif()
      endif()
    endfunction()
    configure_libedit()
  endif()

  if(DEFAULT_PRIVILEGED)
    set(ENABLE_DEFAULT_PRIVILEGED 1)
  endif()
  if(LIBEDIT)
    if(COMPLETION)
      set(ENABLE_COMPLETION 1)
      if(COMPLETION_BUILTIN)
        set(ENABLE_INTERNAL_COMPLETION 1)
      else()
        set(ENABLE_EXTERNAL_COMPLETION 1)
      endif()
    endif()
  else()
    set(SMALL 1)
  endif()
  if(LINENO)
    set(WITH_LINENO 1)
  endif()
  if(LOCALE)
    set(WITH_LOCALE 1)
    if(LOCALE_FOR_PARSER)
      set(WITH_PARSER_LOCALE 1)
    endif()
  endif()
  if(TEST_WORKAROUND)
    set(HAVE_TRADITIONAL_FACCESSAT 1)
  endif()

  if(HAVE_GCC_ATTRIBUTE)
    set(attribute_x "__attribute__(x)")
  else()
    set(attribute_x "/* nothing */")
  endif()
  if(NOT HAVE_INTMAX_WIDTH)
    set(INTMAX_WIDTH "(sizeof(intmax_t)*CHAR_BIT)")
  endif()
  if(NOT HAVE_PRIDMAX)
    try_compile(INTMAX_IS_LONG ${CMAKE_CURRENT_BINARY_DIR}
                ${CMAKE_CURRENT_SOURCE_DIR}/cmake/intmax_is_long.c)
    if(INTMAX_IS_LONG)
      set(PRIdMAX "%ld")
    else()
      try_compile(INTMAX_IS_LONG_LONG ${CMAKE_CURRENT_BINARY_DIR}
                  ${CMAKE_CURRENT_SOURCE_DIR}/cmake/intmax_is_long_long.c)
      if(INTMAX_IS_LONG_LONG)
        set(PRIdMAX "%lld")
      else()
        set(PRIdMAX "%jd")
      endif()
    endif()
  endif()
  if(NOT HAVE_PATH_BSHELL)
    set(_PATH_BSHELL "/bin/sh" CACHE STRING "System shell path")
  endif()
  if(NOT HAVE_PATH_DEVNULL)
    set(_PATH_DEVNULL "/dev/null" CACHE STRING "Null device path")
  endif()
  if(NOT HAVE_PATH_TTY)
    set(_PATH_TTY "/dev/tty" CACHE STRING "TTY device path")
  endif()

  configure_file(cmake/config.h.in config.h)
endfunction()
configure()

include_directories(${CMAKE_CURRENT_BINARY_DIR})
add_subdirectory(src)
